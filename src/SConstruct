######################
# Common environment #
######################

import os
import sys

MIR_ROOT = os.environ.get('MIR_ROOT')
if(MIR_ROOT == None):
    print('MIR_ROOT is not defined. Aborting!')
    Exit(2)

sys.path.append(MIR_ROOT + '/scripts')
import mir_utils

PAPI_ROOT = os.environ.get('PAPI_ROOT')
if(PAPI_ROOT == None):
    print('PAPI_ROOT is not defined. Aborting!')
    Exit(2)

compiler = mir_utils.which('icc')
if(len(compiler) == 0):
    print('Could not find icc. Checking for gcc ...')
    compiler = mir_utils.which('gcc')
if(len(compiler) == 0):
    print('Could not find gcc. Aborting!')
    Exit(2)

env = Environment()
env['CC'] = [compiler[0]]
env['CCFLAGS'] = Split("""
-Wall 
-Werror 
-Wno-unused-function 
-Wno-unused-variable 
-fPIC 
-std=c99 
-D_GNU_SOURCE 
""")

env['CFLAGS'] += ['-I' + PAPI_ROOT+'/include']

#####################
# Debug environment #
#####################

debug = env.Clone();
debug['CCFLAGS'] += Split("""
-O0
-g
-ggdb
-DMIR_ENABLE_DEBUG
""")
debug.VariantDir('debug-build', '.', duplicate=0)
debug_src = debug.Glob('debug-build/*.c')
debug.Library(target = 'mir-debug', source = debug_src)

###################
# Opt environment #
###################

opt = env.Clone();
opt['CCFLAGS'] += ['-O2']
opt.VariantDir('opt-build', '.', duplicate=0)
opt_src = opt.Glob('opt-build/*.c')
opt.Library('mir-opt', source = opt_src)
