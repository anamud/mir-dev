######################
# Common environment #
######################

env = Environment()

env['CCFLAGS'] = Split("""
-Wall 
-Werror 
-Wno-unused-function 
-Wno-unused-variable 
-fPIC 
-std=c99 
-D_GNU_SOURCE 
""")

#####################
# Debug environment #
#####################

debug = env.Clone();
debug['CCFLAGS'] += Split("""
-O0
-g
-gdwarf-2
-DMIR_ENABLE_DEBUG
""")
debug.VariantDir('debug-build', '.', duplicate=0)
debug_src = debug.Glob('debug-build/*.c')
debug.Library(target = 'mir-debug', source = debug_src)

###################
# Opt environment #
###################

opt = env.Clone();
opt['CCFLAGS'] += ['-O3']
opt.VariantDir('opt-build', '.', duplicate=0)
opt_src = opt.Glob('opt-build/*.c')
opt.Library('mir-opt', source = opt_src)
