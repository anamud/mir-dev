import os
app_name = os.getcwd().split(os.sep)[-1]
print('This is a SPECIALIZED BUILD. \n')

######################
# Common environment #
######################

import os
import sys
MIR_ROOT = os.environ.get('MIR_ROOT')
if(MIR_ROOT == None):
    print('MIR_ROOT is not defined. Aborting!')
    Exit(2)

sys.path.append(MIR_ROOT + '/scripts')
import mir_utils

PAPI_ROOT = os.environ.get('PAPI_ROOT')
if(PAPI_ROOT == None):
    print('PAPI_ROOT is not defined. Aborting!')
    Exit(2)

compiler = mir_utils.which('icc')
if(len(compiler) == 0):
    print('Could not find icc. Aborting!')
    Exit(2)

env = Environment()
env['CC'] = [compiler[0]]
env['CCFLAGS'] = Split("""
-std=c99
-Wall 
-Werror 
-Wno-unused-function 
-Wno-unused-variable
-Wno-unused-but-set-variable
-I/opt/intel/mkl/include
""")
env['CFLAGS'] += ['-I' + MIR_ROOT+'/src']
env['CFLAGS'] += ['-I' + MIR_ROOT+'/test/common']
env['LINKFLAGS'] = Split("""
-L/opt/intel/mkl/lib/intel64
-Wl,-rpath=/opt/intel/mkl/lib/intel64
""")
env['LIBS'] = Split("""
pthread
papi
numa
m
mkl_sequential
mkl_core
mkl_rt
""")
env['LIBPATH'] = [MIR_ROOT + '/src']
env['LIBPATH'] += [PAPI_ROOT + '/lib']

#####################
# Debug environment #
#####################

debug = env.Clone();
debug['CCFLAGS'] += Split("""
-O0
-g
-gdwarf-2
-DALLOW_DEBUG_MESSAGES
""")
debug['LIBS'] += ['mir-debug']
debug.VariantDir('debug-build', '.', duplicate=0)
debug_src = debug.Glob('debug-build/*.c')
debug.Program(app_name +'-debug', source = debug_src)

###################
# Opt environment #
###################

opt = env.Clone();
opt['CCFLAGS'] += Split("""
-O3
""")
opt['LIBS'] += ['mir-opt']
opt.VariantDir('opt-build', '.', duplicate=0)
opt_src = opt.Glob('opt-build/*.c')
opt.Program(app_name +'-opt', source = opt_src)

#######################
# Verbose environment #
#######################

verbose = env.Clone();
verbose['CCFLAGS'] += Split("""
-O3
-DALLOW_MESSAGES
-DCHECK_RESULT
""")
verbose['LIBS'] += ['mir-opt']
verbose.VariantDir('verbose-build', '.', duplicate=0)
verbose_src = verbose.Glob('verbose-build/*.c')
verbose.Program(app_name +'-verbose', source = verbose_src)
